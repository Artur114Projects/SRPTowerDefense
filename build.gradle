buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'

version = modVersion
group = modGroup
archivesBaseName = modFileName

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    if (atfile != "null") {
        accessTransformer = file('src/main/resources/META-INF/' + atfile)
    }
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            if (properties.get("coreModClass") != "null") {
                jvmArg '-Dfml.coreMods.load=' + properties.get("coreModClass")
            }
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            if (properties.get("coreModClass") != "null") {
                jvmArg '-Dfml.coreMods.load=' + properties.get("coreModClass")
            }
        }
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'

    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2859'
    compile fileTree(dir: "libs", includes: ['*.jar'])
}

jar {
    manifest {
        attributes([
            "Specification-Title": modId,
            "Specification-Vendor": modId,
            "Specification-Version": modVersion,
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" : modId,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
        if (coreModClass != "null") {
            attributes "FMLCorePlugin": coreModClass
            attributes "FMLCorePluginContainsFMLMod": "true"
        }
    }
}

jar.finalizedBy('reobfJar')

sourceSets {
    main { output.resourcesDir = output.classesDir }
}